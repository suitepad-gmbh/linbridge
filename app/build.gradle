apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: "de.mannodermaus.android-junit5"
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

apply plugin: 'suitepad-gradletools-repos'
apply plugin: 'suitepad-gradletools-signing'

apply from: '../release.gradle'

def getDestinationEmailAddress() {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    return properties.getProperty("sendgrid.mailto")
}

def getSendgridApiKey() {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    return properties.getProperty("sendgrid.apikey")
}

android {
    compileSdkVersion 28

    signingConfigs {
        release {
            signing.load()
        }
    }

    defaultConfig {
        applicationId "de.suitepad.linbridge"
        minSdkVersion 16
        targetSdkVersion 28

        versionCode 1050040
        versionName "1.5.0"

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        buildConfigField 'String', 'SENDGRID_MAIL_TO', "\"${getDestinationEmailAddress()}\""
        buildConfigField 'String', 'SENDGRID_API_KEY', "\"${getSendgridApiKey()}\""
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            signingConfig signingConfigs.release
        }
    }

    testOptions {
        junitPlatform {
            filters {
                engines {
                    include 'spek2'
                }
            }
            jacocoOptions {
                // here goes all jacoco config, for example
                html.enabled = true
                xml.enabled = false
                csv.enabled = false
                unitTests.all {
                    testLogging.events = ["passed", "skipped", "failed"]
                }
            }
        }
    }

}

repositories {
    mavenRemote {
        load "suitepad"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutine_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutine_version"
    implementation 'de.suitepad:linphone-sdk:1.0.0-SNAPSHOT'
    implementation 'de.suitepad:linbridge-api:1.1.0'
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation group: 'com.jakewharton.timber', name: 'timber', version: '4.7.1'
    implementation ('com.github.danysantiago:sendgrid-android:1') {
        exclude group: 'org.apache.httpcomponents.httpclient-android'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
}

// Dagger dependencies
dependencies {
    implementation 'com.google.dagger:dagger-android:2.22.1'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.22.1'
    kapt 'com.google.dagger:dagger-compiler:2.22.1'
}

// Test dependencies
dependencies {
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'

    // spek
    testImplementation "org.spekframework.spek2:spek-dsl-jvm:$spek_version"
    testImplementation "org.spekframework.spek2:spek-runner-junit5:$spek_version"

    // spek requires kotlin-reflect, omit when already in classpath
    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    testImplementation "io.mockk:mockk:$mockk_version"
}

dependencies {
    implementation 'com.google.firebase:firebase-analytics:18.0.3'
    implementation 'com.google.firebase:firebase-crashlytics:17.4.1'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
}

configurations.all {
    resolutionStrategy.force 'com.android.support:support-media-compat:28.0.0'
    resolutionStrategy.force 'com.android.support:support-v4:28.0.0'
}

apply plugin: 'com.google.gms.google-services'